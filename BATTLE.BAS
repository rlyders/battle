1 KEY 1,CHR$(27)+"LIST"+CHR$(13):KEY 2,CHR$(27)+"LIST ":KEY 3,CHR$(27)+"RUN"+CHR$(13):KEY 4,CHR$(27)+"RENUM"+CHR$(13):KEY 5,CHR$(27)+"SAVE "+CHR$(34)+"B":KEY 6,CHR$(27)+"FILES"+CHR$(13):KEY 7,CHR$(27)+"LOAD "+CHR$(34):KEY 8,CHR$(27)+"Run "
2 KEY 9,CHR$(27)+"WIDTH 80"+CHR$(13):KEY 10,CHR$(27)+"SCREEN 0,1,0"+CHR$(13)
3 '@@@@@@@@@@@@@@@@
4 '@@@@ BATTLE @@@@
5 '@@@@@@@@@@@@@@@@
60 '
70 clear:SCREEN 9:KEY OFF
80 DIM L(320),P1(38),P2(38),B1(4),B2(4)',X#(320)
81 '
82 '@@@@ Set Opening Variables @@@@
83 '<Define>-L(0-319)-Land Y's,P1(0-37)-Player one's ship,P2(0-37)-Player two's ship,B1(0-3)-Player one's bubble,B2(0-3)-Player two's bubble
84 '<Define>-L-Player,LC-Land/Peak Color,MC-Mountain Color,AC-Angle Color,
   '(1-2)-Special Player Values,C(1-2)-Player Colors,PT-Power Box Top,
   'PB-Power Box Bottom,PL(1-2)-Player's Power Left,
   'PR(1-2)-Player's Power Right,A(1-2)-Player Angles,LL-Last Player
90 L=2:LC=7:MC=8
100 AC=1:V(1)=1:V(2)=-1:C(1)=2:C(2)=4
110 PT=2:PB=PT+5:FOR L=1 TO 2:P(L)=30:PL(L)=10+150*(L-1):PR(L)=PL(L)+100:NEXT L
120 A(1)=45:A(2)=-45:CLS:RANDOMIZE TIMER
130 '
140 '@@@@ MAIN PORGRAM @@@@
150 GOSUB 490   'CHOOSE PLAYER SITE
160 GOSUB 350   'ASSIGN LAND VARIABLES
170 GOSUB 530   'CREATE IMAGES
180 GOSUB 430   'DRAW LANDSCAPE
190 GOSUB 240   'DRAW SCREEN
200 GOSUB 650   'SWITCH PLAYERS
210 GOSUB 690   'INPUT
220 GOSUB 2000  'FIRE
230 GOTO 200
240 '
250 '@@@@ DRAW SCREEN @@@@
251 '<Loops>-L-Player,X-X Pos For Dividers In Power Box
252 '<Use>-PL(),PR(),PB,AC,PT,C(),MC,P(),X(),Y(),A(),V()
260 FOR L=1 TO 2
270 LINE(PL(L),PT)-(PR(L),PB),AC,BF
280 LINE(PL(L),PT-1)-(PR(L)+1,PB+1),C(L),B
290 LINE(PL(L),PT)-(PL(L)+P(L),PB),MC,BF
300 FOR X=0 TO P(L) STEP 10:LINE(PL(L)+X,PT)-(PL(L)+X,PB),C(L):NEXT X
310 PSET(X(L),Y(L)),0
320 DRAW "C=AC; TA=A(L);":IF L=1 THEN DRAW"R7" ELSE DRAW"L7"
330 LOCATE 1,PR(L)/8+1:PRINT USING"###ø";V(L)*A(L)
340 NEXT L:RETURN
350 '
360 '@@@@ Create Land->L() Values @@@@
361 '(Define>-Yinc:Up/Dwn Value For Land
362 '<loops>-X-Land pos,L-Player
364 '<Use)-L(),X1,X2,Y(),X()
370 X1=INT(RND*100)+60:X2=X1+INT(RND*100)+60
380 L(0)=INT(RND*95)+100
390 FOR X=1 TO 319
    IF X<X1 OR X>X2 THEN
       YINC=INT(RND*3)-1
    ELSE
       YINC=INT(RND*15)-7
    end if
400 IF X=X(1)-10 OR X=X(2)-10 THEN
       FOR aX=X TO X+20
	   L(aX)=L(aX-1)
       NEXT aX
    end if
410 IF L(X-1)+YINC<195 THEN
       L(X)=L(X-1)+YINC
    ELSE
       L(X)=L(X-1)
    end if
420 NEXT X:FOR L=1 TO 2:Y(L)=L(X(L))-7:NEXT L:RETURN
430 '
440 '@@@@ DRAW LANDSCAPE @@@@
442 '<Loops>-X-Land Value
443 '<Use>-LC,L(),MC
450 PSET(0,L(0)),LC
480 FOR X=1 TO 319:LINE(X,L(X))-(X-1,L(X-1)),LC:NEXT X:PAINT(1,199),MC,LC:RETURN
490 '
500 '@@@@ CHOOSE PLAYER SITE @@@@
501 '<Use>-X()
502 '<Loops>-L-Player
510 FOR L=1 TO 2:X(L)=10+200*(L-1)+INT(RND*100):NEXT L
520 RETURN
530 '
540 '@@@@ CREATE IMAGES @@@@
541 '<Use>-X(),Y(),C(),B1,B2,AC,P1,P2
542 '<Loops>-L-Player
550 FOR L=1 TO 2
560 CIRCLE(X(L),Y(L)),2,1
570 PAINT(X(L),Y(L)),C(L),1
580 IF L=1 THEN GET(X(L)-2,Y(L)-2)-(X(L)+2,Y(L)+2),B1
590 IF L=2 THEN GET(X(L)-2,Y(L)-2)-(X(L)+2,Y(L)+2),B2
591 LINE(X(L)-2,Y(L)-2)-(X(L)+2,Y(L)+2),0,BF
600 CIRCLE(X(L),Y(L)),9,AC
610 PAINT(X(L),Y(L)),C(L),AC
620 IF L=1 THEN GET(X(L)-9,Y(L)-7)-(X(L)+9,Y(L)+7),P1
630 IF L=2 THEN GET(X(L)-9,Y(L)-7)-(X(L)+9,Y(L)+7),P2
640 NEXT L:RETURN
650 '
660 '@@@@ SWITCH PLAYERS @@@@
661 '<Use>-L,LL
670 IF L=1 THEN L=2:LL=1 ELSE L=1:LL=2
680 RETURN
690 '
700 '@@@@ INPUT @@@@
701 '<Use>-A(),P()
702 '<Define>-A$-Key Pressed,A-Angle,P-Power,AV-Angle Value,PV-Power Value,BV-Ship Value
710 AV=0:PV=0:A=A(L):P=P(L)
720 A$=INKEY$:ON LEN(A$)+1 GOTO 720,740,760
740 IF A$=CHR$(13) THEN P(L)=P:A(L)=A:RETURN
760 IF LEN(A$)=2 THEN A$=RIGHT$(A$,1) ELSE 720
770 IF A$=CHR$(72) THEN AV=1
780 IF A$=CHR$(80) THEN AV=-1
790 IF A$=CHR$(77) THEN PV=1
800 IF A$=CHR$(75) THEN PV=-1
801 IF A$=CHR$(71) THEN SV=1
802 IF A$=CHR$(79) THEN SV=-1
810 IF PV THEN GOSUB 830
820 IF AV THEN GOSUB 940
821 IF SV THEN GOSUB XXX
823 GOTO 720
830 '
840 '@@@@ DISPLAY POWER @@@@
841 '<Use>-PL(),PR(),P(),PV,PT,PB,PC,C(),MC
842 '<Define>-PL-Power Left
850 P=P(L)
860 IF (P=0 AND PV=-1) OR (P=100 AND PV=1) THEN PV=0:PLAY"MF O3 C64":RETURN
880 IF PV=-1 THEN LINE (PL(L)+P,PT)-(PL(L)+P,PB),AC,B:GOTO 920
900 IF FIX((P+PV)/10)=(P+PV)/10 THEN PC=C(L) ELSE PC=MC
910 LINE (PL(L)+P+1,PT)-(PL(L)+P+1,PB),PC,B
920 P=P+PV:PV=0:P(L)=P:RETURN
940 '
950 '@@@@ DISPLAY ANGLE @@@@
951 '<Use>-A(),C(),X(),Y(),V(),AV
952 '<Define>-A-Angle
960 A=A(L)
970 IF A<=-360 THEN A=0
980 IF A=>360 THEN A=-0
990 PSET(X(L),Y(L)),0
1000 DRAW "C=C(L); TA=A;":IF L=1 THEN DRAW"R7" ELSE DRAW"L7"
1010 A=A+V(L)*AV
1020 PSET(X(L),Y(L)),0
1030 DRAW "C=AC; TA=A;":IF L=1 THEN DRAW"R7" ELSE DRAW"L7"
1040 LOCATE 1,PR(L)/8+1:PRINT USING"###ø";V(L)*A
1050 AV=0:A(L)=A:RETURN
1060 '
1070 '@@@@ MOVE PLAYER UP @@@@
2000 '
2010 '@@@@ SET INITIAL POSITION FOR BUBBLE @@@@
2011 '<Use>-X(),V(),A(),P(),L,LL,B1,B2
2012 '<Define>-X1-Initial X,Y1-Initial Y,T-Initial Time,Yinc-Bubble Y Increment,XINC-Bubble X Increment,LX-Last X,LY-Last Y,X-Bubble X Pos,Y-Bubble Y Pos
2020 X1=X(L)+V(L)*9*COS(A(L)*22/7/180)
2030 Y1=Y(L)+V(LL)*7.5*SIN(A(L)*22/7/180)
2040 YPB=0:T=.1-.5/(P(L)*200)
2050 YINC=P(L)*SIN((180*(L-1)+A(L))*22/7/180)
2060 XINC=P(L)*COS((180*(L-1)+A(L))*22/7/180)
2070 S=0:S=S+T:X=X1+XINC*S:Y=-(YINC*S-4.9*S^2-Y1)
2080 LX=X:LY=Y:IF L=1 THEN PUT(X,Y),B1 ELSE PUT(X,Y),B2
2090 GOTO 2120
2091 '
2100 '@@@@ FIRE -- BUBBLE MOVEMENT @@@@
2101 '<Use>-S,T,X,X1,XINC,Y,Y1,YINC,PB,L(),LL,X(),Y(),L,B1,B2,LX,LY
2110 S=S+T:X=X1+XINC*S:Y=-(YINC*S-4.9*S^2-Y1)
2120 IF Y<PB THEN IF YPB=0 THEN YPB=1:GOSUB 2180:GOTO 2110 ELSE 2110
2130 IF X<1 OR X>319 THEN GOSUB 2180:RETURN ELSE IF Y>L(X) OR Y>199-7 THEN GOSUB 2180:GOSUB 2260:RETURN
2140 IF X>X(LL)-9 AND X<X(LL)+9 AND Y>Y(LL)-7 AND Y<Y(LL)+7 THEN GOSUB 2180:GOSUB 2220:RETURN
2150 IF YPB=0 THEN IF L=1 THEN PUT(LX,LY),B1 ELSE PUT(LX,LY),B2
2151 IF L=1 THEN PUT(X,Y),B1 ELSE PUT(X,Y),B2
2160 YPB=0:LX=X:LY=Y:GOTO 2110
2170 '
2180 '@@@@ ERASE BUBBLE AFTER HIT @@@@
2181 '<Use>-L,LX,LY,B1,B2
2190 IF L=1 THEN PUT(LX,LY),B1 ELSE PUT(LX,LY),B2
2200 RETURN
2210 '
2220 '@@@@ ADD POINTS
2230 BEEP:SCORE(L)=SCORE(L)+POINTS
2240 RETURN
2250 '
2260 '@@@@ Destroy Land
2270 '<Use>-X
2280 '<Loops>-XX-Land X Pos
2290 BEGX=X-ABS((X-LX)*(Y-LY)*(RND*3+1)):ENDX=X+ABS((X-LX)*(Y-LY)*(RND*3+1))
2291 IF BEGX<1 THEN BEGX=1
2292 IF ENDX>319 THEN ENDX=319
2295 LLXX=L(BEGX-1):FOR XX=BEGX TO ENDX
2300 YY=L(XX)+ABS((X-LX)*(Y-LY)*(RND*3+1))+(RND*3+1)/ABS(X-XX)
2301 IF YY>199 THEN YY=200
2310 LINE(XX,L(XX))-(XX,YY),0
2311 LINE(XX,L(XX))-(XX-1,LLXX),0:LLXX=L(XX):L(XX)=YY
2312 LINE(XX,L(XX))-(XX-1,L(XX-1)),LC
2320 NEXT XX
2330 LINE(XX,L(XX))-(XX-1,LLXX),0
2340 LINE(XX,L(XX))-(XX-1,L(XX-1)),LC
2350 RETURN
